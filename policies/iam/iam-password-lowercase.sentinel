# This policy requires that the `require_lowercase_characters` attribute of the `aws_iam_account_password_policy`
# resource is according to CIS standards.

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

# Imports
import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                              "iam-password-lowercase",
	"resource_aws_iam_account_password_policy": "aws_iam_account_password_policy",
	"require_lowercase_characters":             "require_lowercase_characters",
	"message":                                  "Attribute 'require_lowercase_characters' must be equal to true for 'aws_iam_account_password_policy' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/iam-controls.html#iam-12 for more details.",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_iam_account_password_policy).resources
violations = collection.reject(resources, func(res) {
	key = "values.require_lowercase_characters"
	return maps.has(res, key) and maps.get(res, key) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
